D.8.2.11 texring
................
Procedure from library latex.lib (see latex_lib).

Usage:
texring(fname, r[,L]); fname string, r ring, L list

Return:
if fname="": string, the ring in TeX-typesetting;

otherwise: append this string to the file <fname> and
return nothing.

Note:
preceding ">>" are deleted and suffix ".tex" (if not given)
is added to fname.

The optional list L is assumed to be a list of strings which control,
e.g., the symbol for the field of coefficients.

For more details call texdemo(); (generates a LaTeX2e
file called texlibdemo.tex which explains all features of
texring).

Example:
LIB "latex.lib";
ring r0 = 0,(x,y),dp;                // char = 0, polynomial ordering
texring("",r0);
==> $\Q[x,y]$
//
ring r7 =7,(x(0..2)),ds;             // char = 7, local ordering
texring("",r7);
==> $\Z_{7}[[x_{0},x_{1},x_{2}]]$
//
ring r1 = 0,(x1,x2,y1,y2),wp(1,2,3,4);
texring("",r1);
==> $\Q[x_{1},x_{2},y_{1},y_{2}]$
//
ring rr = real,(x),dp;               // real numbers
texring("",rr);
==> $\R[x]$
//
ring rabc =(0,t1,t2,t3),(x,y),dp;    // ring with parameters
texring("",rabc);
==> $\Q(t_{1},t_{2},t_{3})[x,y]$
//
ring ralg = (7,a),(x1,x2),ds;        // algebraic extension
minpoly = a2-a+3;
texring("",ralg);
==> $\Z_{7}(a)[[x_{1},x_{2}]]$
texring("",ralg,"mipo");
==> $\Z_{7}(a)/(a^{2}-a+3)[[x_{1},x_{2}]]$
//
ring r49=(49,a),x,dp;                // Galois field  
texring("",r49);
==> $\F_{49}[x]$
//
setring r0;                          // quotient ring
ideal i = x2-y3;
qring q = std(i);
texring("",q);
==> $\Q[x,y]/\left(y^{3}-x^{2}\right)
==> $
//
// ------------------ additional features -------------------
ring r9 =0,(x(0..9)),ds;
texring("",r9,1);
==> $\Q[[x_{0},\ldots,x_{9}]]$
texring("",r9,"C","{","^G");
==> $\C\{x_{0},x_{1},x_{2},x_{3},x_{4},x_{5},x_{6},x_{7},x_{8},x_{9}\}^G$
//
ring rxy = 0,(x(1..5),y(1..6)),ds;
intvec v = 5,6;
texring("",rxy,v);
==> $\Q[[x_{1},\ldots,x_{5},y_{1},\ldots,y_{6}]]$

<font size="-1">
&nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; User manual for <a href="http://www.singular.uni-kl.de/"><i>Singular</i></a> version 2-0-4, October 2002,
generated by <a href="http://www.nongnu.org/texi2html/"><i>texi2html</i></a>.
</font>

</body>
</html>
